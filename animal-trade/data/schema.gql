define
    name sub attribute, datatype string;
    measured-quantity sub attribute, datatype double;
    unit-of-measurement sub attribute, datatype string;
    exchange-date sub attribute, datatype date;
    appendix sub attribute, datatype long;
    item-purpose sub attribute, datatype string;
    item-type sub attribute, datatype string; # term
    item-source sub attribute, datatype string; # source
    CITES-participation sub attribute, datatype boolean;
    CITES-participation-type sub attribute, datatype string; # unchanged
    CITES-entry-into-force-date sub attribute, datatype date; # unchanged
    ISO-id sub attribute, datatype string; # unchanged
    country sub entity,
        has name,
        has CITES-participation,
        has CITES-entry-into-force-date,
        has CITES-participation-type,
        has ISO-id,
        plays receiving-country, # receiver
        plays providing-country, # provider
        plays originating-country, # origin
        plays contained-country; # containee

    continent sub entity, # region
        has name,
        plays containing-continent; #container

    traded-item sub entity, # animal-item
        plays exchanged-item, # object
        plays imported-item, # object
        plays exported-item, # object
        plays member-item, # target
        plays quantified-subject, # sized
        has item-type, # handled in attrs
        has item-source, # handled in attrs
        has item-purpose; # handled in attrs
    measurement sub entity, # measure
        has measured-quantity, # quantity
        has unit-of-measurement, # handled in attrs
        plays quantification-measurement; # measurement
    exchange sub relation,
        relates receiving-country, # receiver
        relates providing-country, # provider
        relates exchanged-item, # object
        has exchange-date, # exch-date
        has appendix; # appendix
    export sub exchange,
        relates receiving-country, # done
        relates providing-country, # done
        relates exported-item as exchanged-item, # done
        plays corresponding-export; # correspondence-export (App.java)
    import sub exchange,
        relates receiving-country, # done
        relates providing-country, # done
        relates imported-item as exchanged-item, # done
        plays corresponding-import; # correspondence-import
    import-export-correspondence sub relation, # correspondence
        relates corresponding-import, # done
        relates corresponding-export; # done
    quantification sub relation, # sizing
        relates quantified-subject, # done
        relates quantification-measurement; # done
    taxon sub entity, # category
        has name,
        plays taxonomic-group; # explanation
    taxonomic-class sub taxon, # taxonomy-class
        plays super-taxon; # ancestor
    taxonomic-order sub taxon, # taxonomy-order
        plays super-taxon,
        plays sub-taxon; # descendant
    taxonomic-family sub taxon, # taxonomic-family
        plays super-taxon,
        plays sub-taxon;
    taxonomic-genus sub taxon, # taxonomic-genus
        plays super-taxon,
        plays sub-taxon;
    taxonomic-species sub taxon, # taxonomy-taxon
        plays originated-species,
        plays sub-taxon;
    hierarchy sub relation,
        relates superior, # upper
        relates subordinate; # lower
    categorisation-hierarchy sub hierarchy,
        relates superior-category as superior,
        relates subordinate-category as subordinate;
    taxonomic-hierarchy sub categorisation-hierarchy, # taxonomy-hierarchy
        relates super-taxon as superior-category,
        relates sub-taxon as subordinate-category;
    containment sub hierarchy,
        relates container as superior,
        relates containee as subordinate;
    continent-country-containment sub containment, #location-hierarchy
        relates containing-continent as container, #container
        relates contained-country as containee; #containee
    species-origination sub relation,
        relates originating-country,
        relates originated-species;

    taxon-membership sub relation,
            relates member-item,
            relates taxonomic-group;

    taxonomic-ranking sub rule,
        when {
            (super-taxon: $a, sub-taxon: $b) isa taxonomic-hierarchy;
            (super-taxon: $b, sub-taxon: $c) isa taxonomic-hierarchy;
        },
        then {
            (super-taxon: $a, sub-taxon: $c) isa taxonomic-hierarchy;
        };

    transitive-taxonomic-membership sub rule,
        when {
            (member-item: $a, taxonomic-group: $taxon) isa taxon-membership;
            (sub-taxon: $taxon, super-taxon: $super) isa taxonomic-hierarchy;
        },
        then {
            (member-item: $a, taxonomic-group: $super) isa taxon-membership;
        };
